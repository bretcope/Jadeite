// todos
	- Add mixin call

- Terminals are camelCase or literal symbols
- Non-terminals are PascalCase
- A grammar element followed by "-opt" is optional, and can resolve to Îµ

--- TERMINALS --------------------------------------------

Significant Whitespace
	endOfInput
	endOfLine
	indent
	outdent

Templating Kewords:
	extends
	prepend
	append
	block
	mixin

Templating Elements
	bufferedHtmlComment
	unbufferedHtmlComment
	htmlIdentifier
	htmlText

Code Keywords:
	each
	if
	else
	switch
	case
	model

Code Elements
	codeComment
	codeIdentifier
	integerLiteral
	floatingPointLiteral
	stringLiteral

Punctuation
	#
	.
	|
	||
	&
	&&
	+
	++
	+=
	-
	--
	-=
	*
	*=
	/
	/=
	=
	==
	<
	<<
	<=
	>
	>>
	>=
	,
	;
	!
	!=
	?
	:
	(
	)
	#{
	!{
	{
	}
	#[
	[
	]

--- NON-TERMINALS ----------------------------------------

File
	Template-opt MixinList-opt endOfInput

Template
	ModelDefinition Document

ModelDefinition
	endOfLine ModelDefinition
	model codeIdentifier endOfLine

Document
	DoctypeDefinition-opt DocumentBody

DoctypeDefinition
	endOfLine DoctypeDefinition
	doctype TextBodyElementList endOfLine

DocumentBody
	DocumentBodyElement DocumentBody
	DocumentBodyElement

DocumentBodyElement
	Tag
	BufferedCode
	UnescapedBufferedCode
	UnbufferedCode
	DocumentBlock
	HtmlComment
	endOfLine

DocumentBlock
	indent DocumentBody outdent

Tag
	TagElementDeclaration TagAttributes TagBody

TagElementDeclaration
	htmlIdentifier ClassOrIdList-opt
	ClassOrIdList

ClassOrIdList
	ClassOrId ClassOrIdList
	ClassOrId

ClassOrId
	. htmlIdentifier  // class name
	# htmlIdentifier  // ID

TagAttributes
	( TagAttributesList-opt )

TagAttributesList
	TagAttribute , TagAttributesList
	TagAttribute

TagAttribute
	htmlIdentifier = Code

TagBody
	: Tag                                                // tag expansion
	. endofLine PipelessTextBlock-opt                    // pipeless text
	BufferedCode DocumentBlock-opt                       // escaped buffered code
	UnescapedBufferedCode DocumentBlock-opt              // unescaped buffered code
	TextBodyElementList-opt endOfLine DocumentBlock-opt  // normal body
	/ endOfLine                                          // self-closing tag

PipelessTextBlock
	indent TextBodyElementList outdent

BufferedCode
	= Code endOfLine

UnescapedBufferedCode
	!= Code endOfLine

UnbufferedCode
	- endOfLine UnbufferedCodeBlock
	- Code endOfLine DocumentBlock-opt

UnbufferedCodeBlock
	indent MultiLineCode outdent

MultiLineCode
	Code endOfLine
	Code endOfLine MultiLineCode

HtmlComment
	bufferedHtmlComment endOfLine
	unbufferedHtmlComment endOfLine

TextBodyElementList
	TextBodyElement
	TextBodyElement TextBodyElementList

TextBodyElement
	htmlText
	#[ InterpolatedTagDeclaration ]  // interpolated tag
	#{ Code }                        // interpolated escaped buffered code
	!{ Code }                        // interpolated unescaped buffered code

InterpolatedTag
	#[ InterpolatedTagDeclaration ]

InterpolatedTagDeclaration
	TagElementDeclaration TagAttributes-opt InterpolatedTagBody-opt

InterpolatedTagBody
	: InterpolatedTagDeclaration  // tag expansion
	= Code                        // escaped buffered code
	!= Code                       // unescaped buffered code
	TextBodyElementList           // normal body
	/                             // self-closing tag

Code
	//

MixinList
	//