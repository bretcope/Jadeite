// todos
	- Add mixin call
	- Filters
	- Add include to lexer
	- Add extends to lexer
	- Add named blocks to lexer

- Terminals are camelCase or literal symbols
- Non-terminals are PascalCase
- A grammar element followed by "-opt" is optional, and can resolve to Îµ

--- TERMINALS --------------------------------------------

Significant Whitespace
	endOfInput
	endOfLine
	indent
	outdent

Templating Kewords
	extends
	prepend
	append
	block
	include
	mixin
	&attributes

Templating Elements
	bufferedHtmlComment
	unbufferedHtmlComment
	htmlIdentifier
	htmlText

Code Keywords
	each
	if
	else
	switch
	case
	model

Code Elements
	codeComment
	codeIdentifier
	integerLiteral
	floatingPointLiteral
	stringLiteral

Punctuation
	#
	.
	|
	||
	&
	&&
	+
	++
	+=
	-
	--
	-=
	*
	*=
	/
	/=
	=
	==
	<
	<<
	<=
	>
	>>
	>=
	,
	;
	!
	!=
	?
	:
	(
	)
	#{
	!{
	{
	}
	#[
	[
	]

--- NON-TERMINALS ----------------------------------------

File
	Template-opt MixinList-opt endOfInput

Template
	ModelDefinition Document

ModelDefinition
	endOfLine ModelDefinition
	model codeIdentifier endOfLine

Document
	ExtendsDefinition-opt DocumentBody

ExtendsDefinition
	extends ( ArgumentList )  // expecting params (string viewName, object viewModel)

DocumentBody
	DocumentBodyElement DocumentBody
	DocumentBodyElement

DocumentBodyElement
	DoctypeDefinition
	NamedBlock
	IncludeDefinition
	BufferedCode
	UnescapedBufferedCode
	UnbufferedCode
	DocumentBlock
	HtmlComment
	Tag
	endOfLine

DoctypeDefinition
	doctype TextBodyElementList endOfLine

NamedBlock
	NamedBlockPrefix htmlIdentifier endOfLine DocumentBlock-opt

NamedBlockPrefix
	block append
	block prepend
	block
	append
	prepend

IncludeDefinition
	include ( ArgumentList )  // expecting params (string viewName, object viewModel)

DocumentBlock
	indent DocumentBody outdent

Tag
	TagElementDeclaration TagAttributes-opt AndAttributes-opt TagBody

TagElementDeclaration
	htmlIdentifier ClassOrIdList-opt
	ClassOrIdList

ClassOrIdList
	ClassOrId ClassOrIdList
	ClassOrId

ClassOrId
	. htmlIdentifier  // class name
	# htmlIdentifier  // ID

TagAttributes
	( TagAttributesList-opt )

TagAttributesList
	TagAttribute , TagAttributesList
	TagAttribute

TagAttribute
	htmlIdentifier = Code
	htmlIdentifier != Code
	htmlIdentifier

AndAttributes
	&attributes ( CodeIdentifierList-opt )

TagBody
	: Tag                                                // tag expansion
	. endofLine PipelessTextBlock-opt                    // pipeless text
	BufferedCode DocumentBlock-opt                       // escaped buffered code
	UnescapedBufferedCode DocumentBlock-opt              // unescaped buffered code
	TextBodyElementList-opt endOfLine DocumentBlock-opt  // normal body
	/ endOfLine                                          // self-closing tag

PipelessTextBlock
	indent TextBodyElementList outdent

BufferedCode
	= Code endOfLine

UnescapedBufferedCode
	!= Code endOfLine

UnbufferedCode
	- endOfLine UnbufferedCodeBlock
	- Code endOfLine DocumentBlock-opt

UnbufferedCodeBlock
	indent MultiLineCode outdent

MultiLineCode
	Code endOfLine
	Code endOfLine MultiLineCode

HtmlComment
	bufferedHtmlComment endOfLine
	unbufferedHtmlComment endOfLine

TextBodyElementList
	TextBodyElement
	TextBodyElement TextBodyElementList

TextBodyElement
	htmlText
	#[ InterpolatedTagDefinition ]   // interpolated tag
	#{ Code }                        // interpolated escaped buffered code
	!{ Code }                        // interpolated unescaped buffered code

InterpolatedTag
	#[ InterpolatedTagDefinition ]

InterpolatedTagDefinition
	TagElementDeclaration TagAttributes-opt InterpolatedTagBody-opt

InterpolatedTagBody
	: InterpolatedTagDefinition   // tag expansion
	= Code                        // escaped buffered code
	!= Code                       // unescaped buffered code
	TextBodyElementList           // normal body
	/                             // self-closing tag

MixinList
	MixinDefinition MixinList
	MixinDefinition

MixinDefinition
	endOfLine MixinDefinition
	mixin htmlIdentifier MixinParametersDefinition-opt DocumentBlock

MixinParametersDefinition
	( CodeIdentifierList-opt )

CodeIdentifierList
	codeIdentifier , CodeIdentifierList
	codeIdentifier

Code
	//

ArgumentList
	Expression , ArgumentList
	Expression

Expression
	//